// "hello world"
wf := import("@platforma-sdk/workflow-tengo:workflow")
ll := import("@platforma-sdk/workflow-tengo:ll")
exec := import("@platforma-sdk/workflow-tengo:exec")
assets:= import("@platforma-sdk/workflow-tengo:assets")
xsv := import("@platforma-sdk/workflow-tengo:pframes.xsv")
pframes := import("@platforma-sdk/workflow-tengo:pframes")
maps := import("@platforma-sdk/workflow-tengo:maps")
pSpec := import("@platforma-sdk/workflow-tengo:pframes.spec")


prepareFastaSw := assets.importSoftware("@platforma-open/milaboratories.clonotype-clustering.software:prepare-fasta")
processResultsSw := assets.importSoftware("@platforma-open/milaboratories.clonotype-clustering.software:process-results")
mmseqsSw := assets.importSoftware("@platforma-open/soedinglab.software-mmseqs2:mmseqs2")

setVisibility := func(spec, visibility) {
	return maps.deepMerge(spec, {
		annotations: {
			"pl7.app/table/visibility": visibility ? "default" : "optional"
		}
	})
}

wf.prepare(func(args) {
	bundleBuilder := wf.createPBundleBuilder()
	bundleBuilder.ignoreMissingDomains() // to make query work for both bulk and single cell data
	bundleBuilder.addAnchor("main", args.datasetRef) 
	
	// abundance
	bundleBuilder.addSingle({
		axes: [ { anchor: "main", idx: 0 }, { anchor: "main", idx: 1 }],
		annotations: {
				"pl7.app/isAbundance": "true",
				"pl7.app/abundance/normalized": "false",
				"pl7.app/abundance/isPrimary": "true"
			}
		},
		"abundance")

	// pull sequences; for single cell data, we pull both chains
	bundleBuilder.addMulti(args.sequenceRef, "sequences", {
		overrides: {
			domain: {
				 // to pull both "A" and "B" chains in single cell data; not used for bulk data
				"pl7.app/vdj/scClonotypeChain": undefined
			}
		}
	})

	// @TODO: Implement in the future
	// // pull genes
	// for gene in ["V", "J"] {
	// 	bundleBuilder.addSingle({
	// 			axes: [{ anchor: "main", idx: 1 }],
	// 			name: "pl7.app/vdj/geneHit",
	// 			domain: {
	// 				"pl7.app/vdj/reference": gene + "Gene",
	// 				"pl7.app/vdj/scClonotypeChain": "A",
	// 				"pl7.app/vdj/scClonotypeChain/index": "primary"
	// 			}
	// 		},
	// 	 gene + "Gene")
	// }

	// mmseqs2 uses same sequence for centroids, so we use same clonotypeKey as a clusterId
	// and we need to pull corresponding labels from the result pool
	bundleBuilder.addSingle(
		{
			axes: [{ anchor: "main", idx: 1 }],
			name: "pl7.app/label"
		}, "clonotypeKeyLabels") 
	
	return {
		columns: bundleBuilder.build()
	}
})

wf.body(func(args) {
	blockId := wf.blockId().getDataAsJson()
	
	columns := args.columns
	datasetSpec := columns.getSpec(args.datasetRef)
	abundanceSpec := columns.getSpec("abundance")

	// sequence columns
	sequences := columns.getColumns("sequences")
	if len(sequences) == 0 || len(sequences) > 2 {
		ll.panic("expected exactly one or two sequence columns, got ", sequences)
	}

    isSingleCell := datasetSpec.axesSpec[1].name == "pl7.app/vdj/scClonotypeKey"
	if isSingleCell && len(sequences) != 2 {
		ll.panic("expected two sequence columns for single cell data")
	}

	// primary chain sequence column or just sequence column for bulk data
	sequencePrimary := undefined
	// secondary chain sequence column (for sc data)
	sequenceSecondary := undefined

	if isSingleCell {
		if sequences[0].spec.domain["pl7.app/vdj/scClonotypeChain"] == "A" {
			sequencePrimary = sequences[0]
			sequenceSecondary = sequences[1]
		} else {
			sequencePrimary = sequences[1]
			sequenceSecondary = sequences[0]
		}
	} else {
		sequencePrimary = sequences[0]
	}	
	
	/***** Step 1: do clustering on the clonotype level *****/

	// input table
	seqTable := columns.xsvTableBuilder()
	seqTable.setAxisHeader(datasetSpec.axesSpec[1].name, "clonotypeKey")
	
	seqTable.add(sequencePrimary.key, {header: "sequence"})
	if isSingleCell && args.clusterBothChains {
		seqTable.add(sequenceSecondary.key, {header: "sequence_second"})
	}
	
	seqTable = seqTable.build("tsv")

	// prepare fasta file
	fasta := exec.builder().
		software(prepareFastaSw).
		addFile("input.tsv", seqTable).
		saveFile("output.fasta").
		run()

	// run mmseqs2
	// NOTE: mmseqs2 produces different results for same input (different line order),
	// so we save the result in the output to prevent CID conflict by deduplication
	mmseqs := exec.builder().
		software(mmseqsSw).
		dontSaveStdoutOrStderr(). // important to avoid CID conflict problems coming from different stdout output on same datasets
		arg("easy-cluster").
		arg("input.fasta").
		arg("result").
		arg("tmp").
		arg("--min-seq-id").arg(string(args.identity)).
		arg("-c").arg(string(args.identity)).
		arg("--cov-mode").arg("1").
		addFile("input.fasta", fasta.getFile("output.fasta")).
		saveFile("result_cluster.tsv").
		run()

	clusters := mmseqs.getFile("result_cluster.tsv")


	/******* Step 2: aggregate all data and generate results *******/
	cloneTable := columns.xsvTableBuilder()
	cloneTable.add("abundance", {header: "abundance"})
	
	cloneTable.add(sequencePrimary.key, {header: "sequence"})
	if isSingleCell && args.clusterBothChains {
		cloneTable.add(sequenceSecondary.key, {header: "sequence_second"})
	}
	
	cloneTable.add("clonotypeKeyLabels", {header: "clonotypeKeyLabel"})
	// cloneTable.add("VGene", {header: "VGene"})
	// cloneTable.add("JGene", {header: "JGene"})
	cloneTable.setAxisHeader(abundanceSpec.axesSpec[0].name, "sampleId")
	cloneTable.setAxisHeader(abundanceSpec.axesSpec[1].name, "clonotypeKey")
	cloneTable = cloneTable.build("tsv")


	result := exec.builder().
		software(processResultsSw).
		addFile("clusters.tsv", clusters).
		addFile("cloneTable.tsv", cloneTable).
		saveFile("abundances.tsv").
		saveFile("cluster-to-seq.tsv").
		saveFile("clone-to-cluster.tsv").
		saveFile("abundances-per-cluster.tsv"). // cluster to summed abundances
		run()


	abundances := result.getFile("abundances.tsv")
	clusterIdAxisSpec := {
		name: "pl7.app/vdj/clusterId",
		type: "String",
		domain: maps.deepMerge(datasetSpec.axesSpec[1].domain, 
		{
			"pl7.app/vdj/clustering/algorithm": "mmseqs2",
			"pl7.app/vdj/clustering/blockId": blockId
			// @TODO: add clustering parameters here
		}),
		annotations: {
			"pl7.app/label": "Cluster Id",
			"pl7.app/table/visibility": "default",
			"pl7.app/table/orderPriority": "990000"
		}
	}

	ac := {
		name: abundanceSpec.name,
		valueType: abundanceSpec.valueType,
		domain: abundanceSpec.domain,
		annotations: abundanceSpec.annotations
	}
	abundancesPf := xsv.importFile(abundances, "tsv", {
		axes: [{
			column: "sampleId",
			spec: abundanceSpec.axesSpec[0]
		}, {
			column: "clusterId",
			spec: clusterIdAxisSpec
		}],
		columns: [{
			column: "abundance",
			spec: ac
		},
		{
			column: "abundance_normalized",
			spec: maps.deepMerge(ac, {
				annotations: {
					"pl7.app/abundance/normalized": "true"
				}
			})
		}]
	}, {splitDataAndSpec: true})


	abundancesPerCluster := result.getFile("abundances-per-cluster.tsv")

	abundancesPerClusterPf := xsv.importFile(abundancesPerCluster, "tsv", {
		axes: [{
			column: "clusterId",
			spec: clusterIdAxisSpec
		}],
		columns: [{ // modify label according to the data (use a suffix, i.e: Original label + " per cluster"
			column: "abundance_per_cluster",
			spec: maps.deepMerge(ac, { // re-use abundancePf specs in ac, but change column label
				annotations: {
				"pl7.app/label": abundanceSpec.annotations["pl7.app/label"] + " per cluster"
				}
			})
		}]
	}, {splitDataAndSpec: true})

	cloneToCluster := result.getFile("clone-to-cluster.tsv")
	cloneToClusterPf := xsv.importFile(cloneToCluster, "tsv", {
		axes: [{
			column: "clonotypeKey",
			spec: datasetSpec.axesSpec[1]
		}],
		columns: [{
			column: "clusterId",
			spec: setVisibility({
				name: "pl7.app/vdj/clusterId",
				valueType: "String",
				domain: clusterIdAxisSpec.domain,
				annotations: clusterIdAxisSpec.annotations
			}, false)
		}]
	}, {splitDataAndSpec: true})

	cloneToClusterLinkPf := xsv.importFile(cloneToCluster, "tsv", {
		axes: [{
			column: "clonotypeKey",
			spec: datasetSpec.axesSpec[1]
		}, {
			column: "clusterId",
			spec: clusterIdAxisSpec
		}],
		columns: [{
			column: "link",
			spec: setVisibility({
				name: "pl7.app/vdj/link",
				valueType: "Int",
				annotations: {
					"pl7.app/label": "Clone to cluster link",
					"pl7.app/isLinkerColumn": "true"
				}
			}, false)
		}]
	}, {splitDataAndSpec: true})
	
	clusterToSeq := result.getFile("cluster-to-seq.tsv")

	seqColumns := [{
		column: "sequence",
		spec: setVisibility({
			name: sequencePrimary.spec.name,
			valueType: sequencePrimary.spec.valueType,
			domain: sequencePrimary.spec.domain,
			annotations: sequencePrimary.spec.annotations
		}, true)
	},
	{
		column: "size",
		spec: {
			name: "pl7.app/vdj/clustering/clusterSize",
			valueType: "Int",
			annotations: {
				"pl7.app/label": "Cluster Size"
			}
		}
	}, {
		column: "clonotypeKeyLabel",
		spec: {
			name: "pl7.app/label",
			valueType: "String",
			annotations: {
				"pl7.app/label": "Cluster Id"
			}
		}
	}]

	if isSingleCell && args.clusterBothChains {
		seqColumns = append(seqColumns, {
			column: "sequence_second",
			spec: setVisibility({
				name: sequenceSecondary.spec.name,
				valueType: sequenceSecondary.spec.valueType,
				domain: sequenceSecondary.spec.domain,
				annotations: sequenceSecondary.spec.annotations
			}, true)
		})
	}
	clusterToSeqPf := xsv.importFile(clusterToSeq, "tsv", {
		axes: [{
			column: "clusterId",
			spec: clusterIdAxisSpec
		}],
		columns: seqColumns
	}, {splitDataAndSpec: true})
		
	// building outputs & exports
	trace := pSpec.makeTrace(abundanceSpec,
		{
			type: "milaboratories.clonotype-clustering",
		 	importance: 30,
		 	label: "Clonotype clusters"
		})

	opf := pframes.pFrameBuilder()
	for k, v in clusterToSeqPf {
		if k == "clonotypeKeyLabel" {
			 // avoid putting label into the outputs as it will be already in exports; 
			 // in future should be safe to add once PlAgDataTable bug is fixed
			continue
		}
		opf.add(k, trace.inject(v.spec), v.data)
	}
	opf = opf.build()

	epf := pframes.pFrameBuilder() 
	for pf in [abundancesPf, cloneToClusterPf, cloneToClusterLinkPf, clusterToSeqPf, abundancesPerClusterPf] {
		i := 0
		for k, v in pf {
			epf.add(k + string(i), trace.inject(v.spec), v.data) // label, specs, data
			i++
		}
	}
	epf = epf.build()

	clusterAbundanceSpec := trace.inject(abundancesPf["abundance"].spec)

	return {
		outputs: {
			// for table
			clustersPf: pframes.exportFrame(opf),
			// pf for plots
			pf: pframes.exportFrame(epf),
			// specs to outputs for visualization purposes
			clusterAbundanceSpec: clusterAbundanceSpec,
			
			// save mmseqs2 result in the output to enable deduplication of mmseqs2 run
			// NOTE: mmseqs2 produces different results for same input (different line order)
			mmseqs: clusters
		},
		exports: {
			pf: epf
		}
	}
})

