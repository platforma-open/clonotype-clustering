// "hello world"
wf := import("@platforma-sdk/workflow-tengo:workflow")
exec := import("@platforma-sdk/workflow-tengo:exec")
assets:= import("@platforma-sdk/workflow-tengo:assets")
xsv := import("@platforma-sdk/workflow-tengo:pframes.xsv")
text := import("text")
pframes := import("@platforma-sdk/workflow-tengo:pframes")
pSpec := import("@platforma-sdk/workflow-tengo:pframes.spec")

pfClustersConv := import(":pf-clusters-conv")
pfUMAPConv := import(":pf-umap-conv")
pfTSNEConv := import(":pf-tsne-conv")
pfMetricsConv := import(":pf-metrics-conv")
json := import("json")

wf.prepare(func(args) {

	clonotypingRunId := args.clonotypingRunId
	dataType := args.dataType
	chain := args.chain
	
	// Get Pcolumns matching the specified values
	query := func(label) {
		bulkMatcher := []
		// define elements to search in domain sections
		domain := {
					"pl7.app/alphabet": "aminoacid",
					"pl7.app/vdj/feature": "CDR3"
					}
		if dataType == "singleCell" {
			// Pending to add domain somewhere
			// ALERT
			domain["pl7.app/vdj/scClonotypeChain/index"] = "primary"
		} else if dataType == "scFv" {
			domain["pl7.app/vdj/clonotypingRunId"] = clonotypingRunId
		}

		// define whole query 
		r := {
			type: "And",
			operands:  [
				// Exact match in annotation field
				{
					type: "Annotation",
					annotation: "pl7.app/label",
					value: label
				},
				// Exact match in elements defined before in domain fields
				{
					type: "Domain",
					domain: domain,
					domainExactMatch: false
				}
			]
		}

		if dataType == "bulk" {
			// we have to create an axes key matcher
			bulkMatcher = [{
								type: "AxesKeys",
								axesKeys: [
									{
									name: "pl7.app/vdj/clonotypeKey",
									domain: {"pl7.app/vdj/chain": chain},
									domainExactMatch: false
									}
								],
								partialMatch: true  // true allows subset of axes to match
							},
							{
								type: "AxesKeys",
								axesKeys: [
									{
									name: "pl7.app/vdj/clonotypeKey",
									domain: {"pl7.app/vdj/clonotypingRunId": clonotypingRunId},
									domainExactMatch: false
									}
								],
								partialMatch: true  // true allows subset of axes to match
							}]

			r["operands"] = r["operands"] + bulkMatcher
			// old version
			// domain["pl7.app/vdj/chain"] = chain
			// domain["pl7.app/vdj/clonotypingRunId"] = clonotypingRunId
		}
		return r
	}
	
	ops := {
		single: true
	}

	// Defined different returns for single-cell and bulk 
	if dataType == "singleCell" {
		// In single-cell datasets have info for both chains and user can 
		// define specific chain or both
		outputs := {}
		if chain == "Both chains" {
				outputs["selectedChains1"] = wf.query(query("Heavy CDR3 aa Primary"), ops) 
				outputs["selectedChains2"] = wf.query(query("Light CDR3 aa Primary"), ops)
		} else {
			outputs["selectedChains"] = wf.query(query(chain), ops) 
		}
		return  outputs

	} else if dataType == "scFv" {
		// In scFv datasets datasets have info for both chains and user can 
		// define specific chain or both
		outputs := {}
		if chain == "Both chains" {
				outputs["selectedChains1"] = wf.query(query("Heavy CDR3 aa"), ops) 
				outputs["selectedChains2"] = wf.query(query("Light CDR3 aa"), ops)
				// old scFv version query
				// outputs["selectedChains1"] = wf.query(query("CDR3 aa (heavy)"), ops) 
				// outputs["selectedChains2"] = wf.query(query("CDR3 aa (light)"), ops)
		} else {
			outputs["selectedChains"] = wf.query(query(chain), ops) 
		}
		return  outputs

	} else if dataType == "bulk" {
		// In bulk datasets usually are split by chain and users main 
		// selection already indicates it
		return  { 
			selectedChains: wf.query(query("CDR3 aa"), ops)
		}
	} 
})

wf.body(func(args) {
	clonotypingRunId := args.clonotypingRunId
	chain := args.chain
	dataType := args.dataType
	metric := args.metric
	resolution := args.resolution

	// Convert chain selection to Pframe
	selectedChainsPf := []
	selectedChainsSpec := []
	if dataType == "bulk" {
		selectedChains := args.selectedChains
		selectedChainsSpec = [selectedChains.spec]

		selectedChainsPf = [selectedChains]
	} else {
		selectedChainsSpec = [args.selectedChains1.spec, args.selectedChains2.spec]
		if chain == "Both chains" {
			selectedChainsPf = [args.selectedChains1, args.selectedChains2]
		} else {
			selectedChainsPf = [args.selectedChains]
		}
		
	}

	// Make a copy of one of the specs and fill it with more data
	combinedSpec := copy(selectedChainsSpec[0])

	// Fill specs
	// @TODO: update and reduce when old MiXCR and scFv stop to be used
	if combinedSpec.axesSpec[0].domain["pl7.app/vdj/clonotypingRunId"] == undefined {
		combinedSpec.axesSpec[0].domain["pl7.app/vdj/clonotypingRunId"] = combinedSpec.domain["pl7.app/vdj/clonotypingRunId"]
	}
	// Only bulk data has chain info
	if combinedSpec.domain["pl7.app/vdj/chain"] != undefined {
		combinedSpec.axesSpec[0].domain["pl7.app/vdj/chain"] = combinedSpec.domain["pl7.app/vdj/chain"]
	}
	// For SC and scFv we can have multiple scClonotypeChain values clustered
	if combinedSpec.domain["pl7.app/vdj/scClonotypeChain"] != undefined {
		// we assume the posibility of having concatenated multiple chains from same index
		if len(selectedChainsSpec) > 1 {
			tempLabel := []
			for s in selectedChainsSpec{
				tempLabel = tempLabel + [s.domain["pl7.app/vdj/scClonotypeChain"]]
			}
			combinedSpec.axesSpec[0].domain["pl7.app/vdj/scClonotypeChain"] = string(json.encode(copy(tempLabel)))
		} else {
			combinedSpec.axesSpec[0].domain["pl7.app/vdj/scClonotypeChain"] = string(json.encode([combinedSpec.domain["pl7.app/vdj/scClonotypeChain"]]))
		}
	}
	// sc-data has index info but scFv not
	if combinedSpec.domain["pl7.app/vdj/scClonotypeChain/index"] != undefined {
		combinedSpec.axesSpec[0].domain["pl7.app/vdj/scClonotypeChain/index"] = combinedSpec.domain["pl7.app/vdj/scClonotypeChain/index"]
	}
	// single-cell and scFv data contain receptor information
	if combinedSpec.domain["pl7.app/vdj/receptor"] != undefined {
		combinedSpec.axesSpec[0].domain["pl7.app/vdj/receptor"] = combinedSpec.domain["pl7.app/vdj/receptor"]
	}
	
	csv := xsv.exportFrame(selectedChainsPf, "csv", {})

	// get sequence ID labels (future column names)
	sequenceIds := []
	for pcol in selectedChainsPf {
		sequenceIds = sequenceIds + [pcol.spec.annotations["pl7.app/label"]]
	}

	clonotypeClustering := exec.builder().
		software(assets.importSoftware("@platforma-open/milaboratories.clonotype-clustering.software:clonotype-clustering")).
		addFile("input.csv", csv).
		arg("--input").arg("input.csv").
		arg("--seq_column").arg(string(sequenceIds)).
		arg("--output_clusters").arg("clusters.csv").
		arg("--output_umap").arg("umap.csv").
		arg("--output_tsne").arg("tsne.csv").
		arg("--metric").arg(metric).
		arg("--resolution").arg(string(resolution)).
		saveFile("clusters.csv").
		saveFile("umap.csv").
		// saveFile("tsne.csv").
		saveFile("cluster_summary_metrics.csv").
		printErrStreamToStdout().
		saveStdoutContent().
		cache(24 * 60 * 60 * 1000).
		run()

	clustersImportParams := pfClustersConv.getColumns(combinedSpec, resolution, metric)
	clustersPf := xsv.importFile(clonotypeClustering.getFile("clusters.csv"), "csv", clustersImportParams)

	UMAPImportParams := pfUMAPConv.getColumns(combinedSpec, metric)
	UMAPPf := xsv.importFile(clonotypeClustering.getFile("umap.csv"), "csv", UMAPImportParams)

	// TSNEImportParams := pfTSNEConv.getColumns(combinedSpec, metric)
	// TSNEPf := xsv.importFile(clonotypeClustering.getFile("tsne.csv"), "csv", TSNEImportParams)

	metricsImportParams := pfMetricsConv.getColumns(combinedSpec, metric)
	metricsPf := xsv.importFile(clonotypeClustering.getFile("cluster_summary_metrics.csv"), "csv", metricsImportParams)

	return {
		outputs: {
			clustersPf: pframes.exportFrame(clustersPf),
			UMAPPf: pframes.exportFrame(UMAPPf),
			metricsPf: pframes.exportFrame(metricsPf)
		},
		exports: {
			clustersPf: {
				spec: clustersPf["clonotypeCluster.spec"],
				data: clustersPf["clonotypeCluster.data"]
			}
		}
	}
})

