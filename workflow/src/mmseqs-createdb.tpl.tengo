self := import("@platforma-sdk/workflow-tengo:tpl")
exec := import("@platforma-sdk/workflow-tengo:exec")
assets:= import("@platforma-sdk/workflow-tengo:assets")

mmseqsSw := assets.importSoftware("@platforma-open/soedinglab.software-mmseqs2:main")

self.validateInputs({
    "__options__,closed": "",
    fasta: "any",
    mem: "string",
    cpu: "number"
})

self.defineOutputs("inputDbFileSet", "stdout")

self.body(func(inputs) {
    createDbBuilder := exec.builder().
        software(mmseqsSw).
        mem(inputs.mem).
        cpu(inputs.cpu).
        printErrStreamToStdout().
        arg("createdb").
        arg("input.fasta").
        arg("input.db").
        arg("--shuffle").arg("0"). // Set to 0 to match previous easy-cluster workflow results
        arg("--createdb-mode").arg("0"). // Set to 0 to avoid soft links that cause problems on windows
        addFile("input.fasta", inputs.fasta).
        // Save all generated input.db.* files
        saveFileSet("db", "^input\\.db.*")

        // From mmseqs createdb --help, [default values]:
        // --shuffle BOOL        Shuffle input database [1]
        // --createdb-mode INT   Createdb mode 0: copy data, 1: soft link data and write new index (works only with single line fasta/q) [0]

    createDb := createDbBuilder.run()

    return {
        inputDbFileSet: createDb.getFileSet("db"),
        stdout: createDb.getStdoutStream()
    }
})